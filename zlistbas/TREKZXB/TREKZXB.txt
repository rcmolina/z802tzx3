function MID$(ByVal s$, ByVal x As Uinteger, ByVal n As Uinteger):return s$(x to x + n - 1):end function
function LEFT$(ByVal s$, ByVal n As Uinteger):return s$(TO n):end function
function RIGHT$(ByVal s$, ByVal n As Uinteger):return s$(len(s$) - n +1 TO):end function

1 REM **  SUPER STAR TREK
2 REM ** BY MIKE MAYFIELD 1974
3 REM ** MODIFIED BY DAVID AHL 1978
4 REM ** CONVERTED TO 40 COLUMNS
5 REM ** BY ELECTRON.GREG 2022-2023
6 REM ** TO 32 COLUMS FOR ZXSPECTRUM
10 REM SST
20 CLEAR
30 GOSUB 6550
40 FOR N=1 TO 2:PRINT:NEXT
45 BRIGHT 1: BORDER 0: PAPER 0: INK 6: CLS 
50 PRINT "--S-U-P-E-R---S-T-A-R---T-R-E-K-"
60 FOR N=1 TO 5: PRINT : NEXT N
65 INK 5
70 PRINT "                 ,------*------,"
80 PRINT " ,-------------   '---  ------'"
90 PRINT "  '-------- --'      / /"
100 PRINT "      ,---' '-------/ /--,"
110 PRINT "       '----------------'": PRINT : INK 2
120 PRINT " THE USS ENTERPRISE --- NCC-1701"
130 FOR N=1 TO 7:PRINT:NEXT:INK 6
135 RANDOMIZE : REM POKE 23692,255
140 X9=RND
145 LX=0:PRINT"PRESS RETURN TO BEGIN ";:GOSUB 6100
150 PRINT:PRINT "GENERATING GALAXY";
160 Z$="                         "
170 DIM G(8,8): DIM C(9,2): DIM K(3,3): DIM NN(3): DIM ZZ(8,8): DIM D(8)
180 T=INT(RND*20+20)*100:T0=T:T9=25+INT(RND*10):D0=0:E=3000:E0=E
190 P=10:P0=P:S9=200:S=0:B9=2:K9=0:XX$="":X0$=" IS "
200 FUNCTION FND(I,SI,SII) AS FLOAT:RETURN SQR((ABS (K(I,1)-SI))^2+(ABS (K(I,2)-SII))^2):END FUNCTION
210 FUNCTION FNR() AS FLOAT:RETURN INT(RND*7.98+1.01):END FUNCTION
220 REM INITIALISE ENTERPRISE'S POSITION
230 QI=FNR():QII=FNR():SI=FNR():SII=FNR()
240 FOR I=1 TO 9:C(I,1)=0:C(I,2)=0:NEXT I
250 C(3,1)=-1:C(2,1)=-1:C(4,1)=-1:C(4,2)=-1:C(5,2)=-1:C(6,2)=-1
260 C(1,2)=1:C(2,2)=1:C(6,1)=1:C(7,1)=1:C(8,1)=1:C(8,2)=1:C(9,2)=1
270 FOR I=1 TO 8:D(I)=0:NEXT I
280 A1$="NAVSRSLRSPHATORSHEDAMCOMXXX"
290 REM CREATE GALAXY
300 REM K3=KLINGONS  B3=STARBASES  S3=STARS
310 FOR I=1 TO 8:PRINT ".";:FOR J=1 TO 8:K3=0:ZZ(I,J)=0:R1=RND
320 IF R1>0.98 THEN K3=3:K9=K9+3:GOTO 350
330 IF R1>0.95 THEN K3=2:K9=K9+2:GOTO 350
340 IF R1>0.8 THEN K3=1:K9=K9+1
350 B3=0:IF RND>0.96 THEN B3=1:B9=B9+1
360 G(I,J)=K3*100+B3*10+FNR():NEXT J:NEXT I:IF K9>T9 THEN T9=K9+1
370 PRINT" DONE!"
380 GOSUB 6160
390 GOSUB 6230
400 PRINT:GOSUB 6120
410 IF B9<>0 THEN GOTO 440
420 IF G(QI,QII)<200 THEN G(QI,QII)=G(QI,QII)+120:K9=K9+1
430 B9=1:G(QI,QII)=G(QI,QII)+10:QI=FNR():QII=FNR()
440 K7=K9:IF B9<>1 THEN XX$="S":X0$=" ARE "
450 PRINT:PRINT"YOUR ORDERS ARE AS FOLLOWS:":PRINT
460 PRINT "DESTROY THE "; INVERSE 1;K9;" KLINGON"; INVERSE 0;" WARSHIPS BEFORE ";
470 PRINT "STARDATE ";T0+T9;".THIS GIVES YOU "; INVERSE 1;T9;" DAYS"; INVERSE 0;".";
480 PRINT "THERE'S "; INVERSE 1;B9;" STARBASE";XX$; INVERSE 0;" IN THE GALAXY ";
490 PRINT "FOR RESUPPLYING & REPAIRING YOUR SHIP."
500 I=RND
510 REM HERE ANY TIME NEW QUADRANT ENTERED
520 Z4=QI:Z5=QII:K3=0:B3=0:S3=0:G5=0:D4=0.5*RND:ZZ(QI,QII)=G(QI,QII)
530 IF QI<1 OR QI>8 OR QII<1 OR QII>8 THEN GOTO 650
540 GOSUB 5850:PRINT:IF T0<>T THEN GOTO 590
550 PRINT"YOUR MISSION BEGINS WITH YOUR STARSHIP"
560 PRINT"IN THE QUADRANT, '";G2$;"'.":quadname$=G2$
570 SRSFLAG=1
580 GOTO 610
590 IF ATAKFLAG=1 THEN GOSUB 6120:ATAKFLAG=0:PRINT
600 SRSFLAG=1:PRINT"ENTERING ";G2$;" QUADRANT...":quadname$=G2$
610 K3=INT(G(QI,QII)*0.01):B3=INT(G(QI,QII)*0.1)-10*K3
620 S3=G(QI,QII)-100*K3-10*B3
630 I=1
640 K(I,1)=0:K(I,2)=0:I=I+1:IF I>3 THEN GOTO 650 ELSE GOTO 640
650 I=1
660 K(I,3)=0:I=I+1:IF I>3 THEN GOTO 670 ELSE GOTO 660
670 Q$=Z$+Z$+Z$+Z$+Z$+Z$+Z$+LEFT$(Z$,17)
680 REM POSITION ENTERPRISE, KLINGONS, STARBASES, & STARS
690 A$=FCOL$+"E"+BCOL$:Z1=SI:Z2=SII:GOSUB 5650:IF K3<1 THEN GOTO 730
700 I%=1
710 GOSUB 5610:A$=ECOL$+"K"+BCOL$:Z1=R1:Z2=R2
720 GOSUB 5650:K(I%,1)=R1:K(I%,2)=R2:K(I%,3)=S9*(0.5+RND):I%=I%+1:IF I%>K3 THEN GOTO 730 ELSE GOTO 710
730 IF B3<1 THEN GOTO 750
740 GOSUB 5610:A$=DCOL$+"B"+BCOL$:Z1=R1:B4=R1:Z2=R2:B5=R2:GOSUB 5650
750 FOR I=1 TO S3:GOSUB 5610:A$=HCOL$+"*"+BCOL$:Z1=R1:Z2=R2:GOSUB 5650:NEXT I
760 GOSUB 3750
770 IF S+E>10 THEN IF E>10 OR D(7)=0 THEN GOTO 860
780 PRINT:PRINT"** FATAL ERROR **"
790 PRINT"YOU'VE STRANDED YOUR SHIP IN SPACE."
800 PRINT"YOU HAVE INSUFFICIENT MANOEUVRING"
810 PRINT"ENERGY, & SHIELD CONTROL IS PRESENTLY"
820 PRINT"INCAPABLE OF CROSS-CIRCUITING TO"
830 PRINT"ENGINE ROOM!!"
840 PRINT:GOSUB 6120
850 GOTO 3540
860 PRINT:SRSFLAG=0:LX=3:PRINT"COMMAND: ";:GOSUB 6100
870 A$=LIIII$:ZIIII$=A$:ATAKFLAG=0
880 IF A$="SLS" THEN SLSFLAG=1
890 IF A$="SLS" THEN PRINT:PRINT "SHORT & LONG RANGE SCAN...":GOTO 3740
900 IF A$<>"KEY" THEN GOTO 930
910 GOSUB 6160:REM KEY TO SRS ICONS
920 GOTO 860
930 COMFLAG=0:IF A$="GAL" AND D(8)>=0 THEN A$="COM":COMFLAG=1
935 IF D(8)<0 AND A$="GAL" THEN PRINT:PRINT "SHIP'S COMPUTER DISABLED":GOTO 860
940 I=1
950 IF LEFT$(A$,3)<>MID$(A1$,3*I-2,3) THEN GOTO 970
960 ON I-1 GOTO 1010,760,1910,2240,2520,2980,3110,4330,3590
970 I=I+1:IF I>9 THEN GOTO 980 ELSE GOTO 950
980 GOSUB 6230
990 GOTO 770
1000 REM COURSE CONTROL
1010 GOSUB 6390
1020 PRINT:LX=5:PRINT"COURSE (1-9): ";:GOSUB 6100
1030 CI=VAL(LIIII$):IF CI=9 THEN CI=1
1040 IF CI>=1 AND CI<9 THEN GOTO 1070
1050 PRINT:PRINT"LT. SULU REPORTS, 'INCORRECT COURSE ";
1060 PRINT"DATA, SIR!'":GOTO 770
1070 XX$="8":IF D(1)<0 THEN XX$="0.2"
1080 SRSFLAG=1:LX=5:PRINT"WARP FACTOR (0-";XX$;"): ";:GOSUB 6100
1090 W1=VAL(LIIII$):IF D(1)<0 AND W1>0.2 THEN GOTO 1140
1100 IF W1>0 AND W1<=8 THEN GOTO 1160
1110 IF W1=0 THEN GOTO 770
1120 PRINT:PRINT"CHIEF ENGINEER SCOTT REPORTS 'THE"
1130 PRINT"ENGINES WON'T TAKE WARP";W1;"!'":GOTO 770
1140 PRINT:PRINT"WARP ENGINES ARE DAMAGED."
1150 PRINT"MAXIMUM SPEED = WARP 0.2"::GOTO 770
1160 N=INT(W1*8+0.5):IF E-N>=0 THEN GOTO 1250
1170 PRINT:PRINT"ENGINEERING REPORTS 'INSUFFICIENT ENERGY";
1180 PRINT"AVA1LABLE FOR WARP";W1;"!'"
1190 IF S<N-E OR D(7)<0 THEN GOTO 770
1200 PRINT:PRINT"DEFLECTOR CONTROL ROOM ACKNOWLEDGES"
1210 SS$=STR$(S)
1220 PRINT "SHIELD ENERGY DEPLOYED IS ";SS$;" UNITS."
1230 GOTO 770
1240 REM KLINGONS MOVE/FIRE
1250 I=1
1260 IF K(I,3)=0 THEN GOTO 1290
1270 A$="   ":Z1=K(I,1):Z2=K(I,2):GOSUB 5650:GOSUB 5610
1280 K(I,1)=Z1:K(I,2)=Z2:A$=ECOL$+"K"+BCOL$:GOSUB 5650
1290 I=I+1:IF I>K3 THEN GOTO 1300 ELSE GOTO 1260
1300 GOSUB 3370:D1=0:D6=W1:IF W1>=1 THEN D6=1
1310 I=1
1320 IF D(I)>=0 THEN GOTO 1370
1330 D(I)=D(I)+D6:IF D(I)>-0.1 AND D(I)<0 THEN D(I)=-0.1:GOTO 1370
1340 IF D(I)<0 THEN GOTO 1370
1350 IF D1<>1 THEN D1=1:PRINT:PRINT"DAMAGE CONTROL REPORT:  "
1360 R1=I:GOSUB 5710:PRINT G2$;" REPA1R COMPLETED"
1370 I=I+1:IF I>8 THEN GOTO 1380 ELSE GOTO 1320
1380 IF RND>0.2 THEN GOTO 1450
1390 R1=FNR():IF RND>=0.6 THEN GOTO 1420
1400 D(R1)=D(R1)-(RND*5+1):PRINT:PRINT"DAMAGE CONTROL REPORTS:"
1410 GOSUB 5710:PRINT G2$;" DAMAGED":GOTO 1450
1420 D(R1)=D(R1)+RND*3+1:PRINT:PRINT"DAMAGE CONTROL REPORTS:"
1430 GOSUB 5710:PRINT G2$;" PARTLY REPA1RED"
1440 REM MOVING STARSHIP
1450 A$="   ":Z1=INT(SI):Z2=INT(SII):GOSUB 5650
1460 XI=C(CI,1)+(C(CI+1,1)-C(CI,1))*(CI-INT(CI)):X=SI:Y=SII
1470 XII=C(CI,2)+(C(CI+1,2)-C(CI,2))*(CI-INT(CI)):QIV=QI:QV=QII
1480 I=1
1490 SI=SI+XI:SII=SII+XII:IF SI<1 OR SI>=9 OR SII<1 OR SII>=9 THEN GOTO 1640
1500 S8=INT(SI)*24+INT(SII)*3-26:IF MID$(Q$,S8,2)="  " THEN GOTO 1560
1510 SI=INT(SI-XI):SII=INT(SII-XII):PRINT:PRINT"WARP ENG.SHUT DOWN AT ";
1520 PRINT"SECTOR";SI;",";SII
1530 PRINT"DUE TO BAD NAVIGATION"
1540 SRSFLAG=1
1550 GOTO 1580
1560 I=I+1:IF I>N THEN GOTO 1570 ELSE GOTO 1490
1570 SI=INT(SI):SII=INT(SII)
1580 A$=FCOL$+"E"+BCOL$:Z1=INT(SI):Z2=INT(SII):GOSUB 5650:GOSUB 1850:T8=1
1590 IF W1<1 THEN T8=0.1*INT(10*W1)
1600 T=T+T8:IF T>T0+T9 THEN GOTO 3540
1610 REM SEE IF DOCKED
1620 GOTO 760
1630 REM EXCEED LIMITS
1640 X=8*QI+X+N*XI:Y=8*QII+Y+N*XII:QI=INT(X/8):QII=INT(Y/8):SI=INT(X-QI*8)
1650 SII=INT(Y-QII*8):IF SI=0 THEN QI=QI-1:SI=8
1660 IF SII=0 THEN QII=QII-1:SII=8
1670 X5=0:IF QI<1 THEN X5=1:QI=1:SI=1
1680 IF QI>8 THEN X5=1:QI=8:SI=8
1690 IF QII<1 THEN X5=1:QII=1:SII=1
1700 IF QII>8 THEN X5=1:QII=8:SII=8
1710 IF X5=0 THEN GOTO 1820
1720 PRINT:PRINT"LT. UHURA REPORTS MESSAGE FROM STARFLEET ";
1730 PRINT"COMMAND: 'PERMISSION TO ATTEMPT CROSSING";
1740 PRINT"OF GALACTIC PERIMETER IS HEREBY ";
1750 PRINT"*DENIED*. SHUT DOWN YOUR ENGINES.'"
1760 PRINT:PRINT"CHIEF ENGINEER SCOTT REPORTS 'WARP ";
1770 PRINT"ENGINES SHUT DOWN AT SECTOR ";SI;",";SII;
1780 PRINT" OF QUADRANT ";QI;",";QII;".'"
1790 SRSFLAG=1
1800 PRINT:GOSUB 6120
1810 IF T>T0+T9 THEN GOTO 3540
1820 IF 8*QI+QII=8*QIV+QV THEN GOTO 1580
1830 T=T+1:GOSUB 1850:GOTO 520
1840 REM MANOEUVRE ENERGY
1850 E=E-N-10:IF E>=0 THEN RETURN
1860 PRINT:PRINT"SHIELD CONTROL SUPPLIES ENERGY TO"
1870 PRINT"COMPLETE THE MANOEUVRE."
1880 S=S+E:E=0:IF S<=0 THEN S=0
1890 RETURN
1900 REM LONG RANGE SCAN
1910 K1=0
1920 IF D(3)<0 THEN PRINT:PRINT"LONG RANGE SENSORS ARE INOPERABLE"
1930 IF D(3)<0 THEN SLSFLAG=0:GOTO 770
1940 PRINT:PRINT"LONG RANGE SCAN FOR QUADRANT ";QI;",";QII;
1950 PRINT:O1$=" +-----+-----+-----+":PRINT O1$;
1960 PRINT "     ###"
1970 I=QI-1:II=QI+1
1980 NN(1)=-1:NN(2)=-2:NN(3)=-3
1990 J=QII-1:JJ=QII+1
2000 IF I>0 AND I<9 AND J>0 AND J<9 THEN NN(J-QII+2)=G(I,J):ZZ(I,J)=G(I,J)
2010 J=J+1:IF J>JJ THEN GOTO 2020 ELSE GOTO 2000
2020 L=1
2030 IF K1=2 AND L=3 THEN INK 5:PRINT ")";:INK 6:PRINT ":";:GOTO 2050
2040 PRINT BCOL$;":";
2050 IF K1=2 AND L=2 THEN INK 5:PRINT "(";:INK 6:GOTO 2070
2060 PRINT " ";
2070 IF NN(L)<0 THEN PRINT"***";:GOTO 2100
2080 G1$=RIGHT$(STR$(NN(L)+1000),3)
2090 G2$=MID$(G1$,1,3):PRINT G2$;
2100 L=L+1:IF L>3 THEN GOTO 2110 ELSE GOTO 2030
2110 PRINT" :";:K1=K1+1
2120 IF K1=1 THEN PRINT "    . . ."
2130 IF K1=3 THEN PRINT "  .   .   ."
2140 IF K1=5 THEN PRINT " .         .";
2150 LET K1=K1+1: PRINT O1$;
2160 IF K1=2 THEN PRINT "   .  .  ."
2170 IF K1=4 THEN PRINT " .  BASES";
2180 IF K1=4 THEN PRINT "  .";
2190 IF K1=6 THEN PRINT "KLINGS STARS"
2200 I=I+1:IF I>II THEN GOTO 2210 ELSE GOTO 1980
2210 K1=0:IF SLSFLAG=1 THEN SLSFLAG=0
2220 GOTO 860
2230 REM PHASER CONTROL
2240 IF D(4)<0 THEN PRINT:PRINT"PHASERS INOPERATIVE":GOTO 770
2250 IF K3>0 THEN GOTO 2280
2260 PRINT:PRINT"SCIENCE OFFICER SPOCK REPORTS 'SENSORS"
2270 PRINT"SHOW NO ENEMY SHIPS IN THIS QUADRANT'":GOTO 770
2280 IF D(8)<0 THEN PRINT:PRINT"COMPUTER FA1LURE HAMPERS ACCURACY"
2290 PRINT:PRINT"PHASERS LOCKED ON TARGET!  "
2300 PRINT:PRINT"ENERGY AVAILABLE = ";E;" UNITS"
2310 LX=5:PRINT "NUMBER OF UNITS TO FIRE: ";:GOSUB 6100
2320 X=VAL(LIIII$):IF X<=0 THEN GOTO 770
2330 IF E-X<0 THEN GOTO 2300
2340 E=E-X:IF D(7)<0 THEN X=X*RND
2350 H1=INT(X/K3):I=1
2360 IF K(I,3)<=0 THEN GOTO 2480
2370 H=INT((H1/FND(I,SI,SII))*(RND+2)):IF H>0.15*K(I,3) THEN GOTO 2410
2380 PRINT:PRINT"SENSORS SHOW NO DAMAGE TO ENEMY"
2390 PRINT"AT ";K(I,1);",";K(I,2)
2400 GOTO 2480
2410 K(I,3)=K(I,3)-H:PRINT
2420 PRINT "";H;" UNIT HIT KLINGON AT ";K(I,1);",";K(I,2)
2430 IF K(I,3)<=0 THEN PRINT" *** KLINGON DESTROYED ***":GOTO 2460
2440 PRINT" (SENSORS SHOW ";INT(K(I,3));" UNITS REMA1NING)"
2450 GOTO 2480
2460 K3=K3-1:K9=K9-1:Z1=K(I,1):Z2=K(I,2):A$="   ":GOSUB 5650
2470 K(I,3)=0:G(QI,QII)=G(QI,QII)-100:ZZ(QI,QII)=G(QI,QII):IF K9<=0 THEN GOTO 3680
2480 I=I+1:IF I>3 THEN GOTO 2490 ELSE GOTO 2360
2490 IF K3>0 THEN PRINT:GOSUB 6120
2500 GOSUB 3370:GOTO 770
2510 REM PHOTON TORPEDO
2520 IF P<=0 THEN PRINT:PRINT"ALL PHOTON TORPEDOES EXPENDED":GOTO 770
2530 IF D(5)<0 THEN PRINT:PRINT"PHOTON TUBES ARE NOT OPERATIONAL":GOTO 770
2540 GOSUB 6390:REM * DIRECTION HELPER *
2550 PRINT:LX=5:PRINT "PHOTON TORPEDO COURSE (1-9): ";:GOSUB 6100
2560 CI=VAL(LIIII$):IF CI=9 THEN CI=1
2570 IF CI>=1 AND CI<9 THEN GOTO 2610
2580 PRINT:PRINT"ENSIGN CHEKOV REPORTS, 'INCORRECT"
2590 PRINT"COURSE DATA, SIR!'"
2600 GOTO 770
2610 XI=C(CI,1)+(C(CI+1,1)-C(CI,1))*(CI-INT(CI)):E=E-2:P=P-1
2620 XII=C(CI,2)+(C(CI+1,2)-C(CI,2))*(CI-INT(CI)):X=SI:Y=SII
2630 PRINT:PRINT"TORPEDO TRACKING:"
2640 X=X+XI:Y=Y+XII:X3=INT(X+0.5):Y3=INT(Y+0.5)
2650 IF X3<1 OR X3>8 OR Y3<1 OR Y3>8 THEN GOTO 2940
2660 PRINT" ";X3;",";Y3:A$="   ":Z1=X:Z2=Y:GOSUB 5810
2670 IF Z3<>0 THEN GOTO 2640
2680 A$=ECOL$+"K"+BCOL$:Z1=X:Z2=Y:GOSUB 5810:IF Z3=0 THEN GOTO 2770
2690 PRINT" *** KLINGON DESTROYED ***"
2700 K3=K3-1:IF K3>0 THEN PRINT:GOSUB 6120
2710 K9=K9-1:IF K9<=0 THEN GOTO 3680
2720 I=1
2730 IF X3=K(I,1) AND Y3=K(I,2) THEN GOTO 2760
2740 I=I+1:IF I>3 THEN GOTO 2750 ELSE GOTO 2730
2750 I=3
2760 K(I,3)=0:GOTO 2920
2770 A$=HCOL$+"*"+BCOL$:Z1=X:Z2=Y:GOSUB 5810:IF Z3=0 THEN GOTO 2810
2780 PRINT"* STAR AT ";X3;",";Y3;" ABSORBED TORPEDO *"
2790 IF K3<>0 THEN PRINT:GOSUB 6120
2800 GOSUB 3370:GOTO 770
2810 A$=DCOL$+"B"+BCOL$:Z1=X:Z2=Y:GOSUB 5810:IF Z3=0 THEN GOTO 2540
2820 PRINT" *** STARBASE DESTROYED ***"
2830 PRINT:GOSUB 6120
2840 B3=B3-1:B9=B9-1
2850 IF B9>0 OR K9>T-T0-T9 THEN GOTO 2900
2860 PRINT:PRINT"THAT DOES IT, CAPTA1N!!  YOU ARE HEREBY"
2870 PRINT"RELIEVED OF COMMAND AND SENTENCED TO 99"
2880 PRINT"STARDATES AT HARD LABOUR ON CYGNUS 12!!"
2890 GOTO 3590
2900 PRINT:PRINT"STARFLEET COMMAND REVIEWING YOUR RECORD"
2910 PRINT"TO CONSIDER COURT MARTIAL!":D0=0
2920 Z1=X:Z2=Y:A$="   ":GOSUB 5650
2930 G(QI,QII)=K3*100+B3*10+S3:ZZ(QI,QII)=G(QI,QII):GOSUB 3370:GOTO 770
2940 PRINT" ** TORPEDO MISSED **"
2950 IF K3<>0 THEN PRINT:GOSUB 6120
2960 GOSUB 3370:GOTO 770
2970 REM SHIELD CONTROL
2980 IF D(7)<0 THEN PRINT:PRINT"SHIELD CONTROL INOPERABLE":GOTO 770
2990 PRINT:PRINT"ENERGY AVA1LABLE =";E+S
3000 LX=5:PRINT"NUMBER OF UNITS TO SHIELDS: ";:GOSUB 6100
3010 X=VAL(LIIII$)
3020 IF X<0 OR S=X THEN PRINT"<SHIELDS UNCHANGED>":GOTO 770
3030 IF X<=E+S THEN GOTO 3070
3040 PRINT:PRINT"SHIELD CONTROL REPORTS 'THIS IS NOT THE"
3050 PRINT"FEDERATION TREASURY.'"
3060 PRINT"<SHIELDS UNCHANGED>":GOTO 770
3070 E=E+S-X:S=X:PRINT:PRINT"DEFLECTOR CONTROL ROOM REPORT:"
3080 PRINT"'SHIELDS NOW AT ";INT(S);" UNITS PER"
3090 PRINT"YOUR COMMAND.'":GOTO 770
3100 REM DAMAGE CONTROL
3110 IF D(6)>=0 THEN GOTO 3260
3120 PRINT:PRINT"DAMAGE CONTROL REPORT NOT AVA1LABLE"
3130 IF D0=0 THEN GOTO 770
3140 D3=CI-CI:FOR I=1 TO 8:IF D(I)<0 THEN D3=D3+0.1
3150 NEXT I:IF D3=0 THEN GOTO 770
3160 D3=D3+D4:IF D3>=1 THEN D3=0.9
3170 PRINT:PRINT"TECHNICIANS STANDING BY TO EFFECT"
3180 PRINT"REPA1RS TO YOUR SHIP;"
3190 PRINT"ESTIMATED TIME TO REPA1R:"
3200 PRINT 0.01*INT(100*D3);"STARDATES"
3210 LX=1:PRINT:PRINT "AUTHORISE THE REPA1R ORDER (Y/N)? ";:GOSUB 6100
3220 A$=LIIII$
3230 IF A$<>"Y" THEN GOTO 770
3240 FOR I=1 TO 8:IF D(I)<0 THEN D(I)=0
3250 NEXT I:T=T+D3+0.1
3260 PRINT : PRINT "SYSTEM           STATE OF REPAIR"
3270 PRINT "---------------- ---------------"
3280 FOR R1=1 TO 8
3290 GOSUB 5710:PRINT "";G2$;LEFT$(Z$,16-LEN(G2$));
3300 D2=INT(D(R1)*10)*0.1
3310 IF D2<0 THEN INK 2:PRINT CCOL$;"DAMAGED     ";D2;BCOL$:INK 6
3320 IF D2>0 THEN PRINT " OPERATIONAL ";D2
3330 IF D2=0 THEN PRINT " OPERATIONAL ";D2
3340 NEXT R1:IF D0<>0 THEN GOTO 3140
3350 GOTO 770
3360 REM KLINGONS SHOOTING
3370 IF K3<=0 THEN RETURN
3380 IF D0<>0 THEN PRINT:PRINT"STARBASE SHIELDS PROTECT THE ENTERPRISE"
3390 IF D0<>0 THEN RETURN
3400 I=1
3410 IF K(I,3)<=0 THEN GOTO 3500
3420 H=INT((K(I,3)/FND(I,SI,SII))*(2+RND)):S=S-H:K(I,3)=K(I,3)/(3+RND)
3430 PRINT:PRINT "";H;" UNIT HIT ENTERPRISE FROM ";K(I,1);",";K(I,2)
3440 IF S<=0 THEN GOTO 3550
3450 PRINT" <SHIELDS DOWN TO ";S;" UNITS>":IF H<20 THEN GOTO 3500
3460 IF RND>0.6 OR H/S<=0.02 THEN GOTO 3500
3470 R1=FNR():D(R1)=D(R1)-H/S-0.5*RND:GOSUB 5710
3480 PRINT:PRINT"DAMAGE CONTROL REPORTS: "
3490 PRINT G2$;" DAMAGED BY THE HIT"
3500 I=I+1:IF I>3 THEN GOTO 3510 ELSE GOTO 3410
3510 ATAKFLAG=1
3520 RETURN
3530 REM END OF GAME
3540 PRINT:PRINT"IT IS STARDATE ";T:GOTO 3590
3550 PRINT:GOSUB 6120
3560 PRINT:PRINT"THE ENTERPRISE HAS BEEN DESTROYED. THE ";
3570 PRINT"FEDERATION WILL BE CONQUERED"
3580 GOTO 3540
3590 PRINT:PRINT"THERE WERE ";K9;" KLINGON BATTLE CRUISERS ";
3600 PRINT"LEFT AT THE END OF YOUR MISSION."
3610 IF B9=0 THEN GOTO 3670
3620 PRINT:PRINT"THE FEDERATION IS IN NEED OF A NEW ";
3630 PRINT"STARSHIP COMMANDER FOR ANOTHER MISSION."
3640 PRINT:PRINT"... IF THERE IS A VOLUNTEER, ";
3650 LX=3:PRINT "STEP FORWARD AND ENTER 'AYE': ";:GOSUB 6100
3660 A$=LIIII$:IF A$="AYE" THEN GOTO 10
3670 STOP
3680 PRINT:GOSUB 6120
3690 PRINT:PRINT"CONGRATULATIONS, CAPTA1N! THE LAST"
3700 PRINT"KLINGON BATTLE CRUISER MENACING THE"
3710 PRINT"FEDERATION HAS BEEN DESTROYED."
3720 PRINT:PRINT"YOUR EFFICIENCY RATING IS";
3730 PRINT INT(1000*(K7/(T-T0))^2):GOTO 3610
3740 REM SHORT RANGE SCAN
3750 IF ATAKFLAG=1 THEN PRINT:GOSUB 6120:ATAKFLAG=0
3760 IF D(2)<0 THEN GOTO 3910
3770 IF SRSFLAG=0 THEN PRINT:PRINT"SHORT RANGE SCAN + SUMMARY DATA"
3780 SRSFLAG=0:ATAKFLAG=0
3790 II=SI-1:II2=SI+1
3800 JJ=SII-1:JJ2=SII+1
3810 IF INT(II+0.5)<1 OR INT(II+0.5)>8 OR INT(JJ+0.5)<1 OR INT(JJ+0.5)>8 THEN GOTO 3830
3820 A$=DCOL$+"B"+BCOL$:Z1=II:Z2=JJ:GOSUB 5810:IF Z3=1 THEN GOTO 3860
3830 JJ=JJ+1:IF JJ>JJ2 THEN GOTO 3840 ELSE GOTO 3810
3840 II=II+1:IF II>II2 THEN GOTO 3850 ELSE GOTO 3800
3850 D0=0:IF Z3=1 THEN GOTO 3860 ELSE GOTO 3880
3860 D0=1:CC$=DCOL$+"DOCKED"+BCOL$:E=E0:P=P0
3870 PRINT:PRINT"SHIELDS DROPPED FOR DOCKING PURPOSES":S=0:GOTO3900
3880 IF K3>0 THEN CC$=CCOL$+"*RED*"+BCOL$:GOTO 3900
3890 CC$=FCOL$+"GREEN"+BCOL$:IF E<E0*0.1 THEN CC$=HCOL$+"AMBER"+BCOL$
3900 IF D(2)>=0 THEN GOTO 3940
3910 PRINT:PRINT"* SHORT RANGE SENSORS ARE OUT *"
3920 IF SLSFLAG=1 THEN GOTO 1900
3930 RETURN
3940 IF K3=0 THEN GOTO 3970
3950 PRINT:PRINT "COMBAT AREA ";
3960 INK 2:PRINT CCOL$;"** CONDITION RED **";BCOL$:INK 6
3970 REM
3980 LOW$="LOW!":PRINT
3990 PRINT "  1 2 3 4 5 6 7 8"
4000 PRINT " +-+-+-+-+-+-+-+-+STARDATE ";INT(T)
4010 I=1
4020 III$=STR$(I)
4030 PRINT "";III$;":";
4040 J1=(I-1)*24+1:J2=(I-1)*24+22
4050 J=J1:COL=6
4060 ZIII$=MID$(Q$,J,2)
4061 IF RIGHT$(ZIII$,1)="K" THEN COL=2
4062 IF RIGHT$(ZIII$,1)="B" THEN COL=4
4063 IF RIGHT$(ZIII$,1)="*" THEN COL=6
4064 IF RIGHT$(ZIII$,1)="E" THEN COL=5
4070 IF J=J1 AND ZIII$<>"  " THEN INK 6:PRINT CHR$(8);":";:INK COL:PRINT RIGHT$(ZIII$,1);:INK 6:GOTO 4100
4080 IF J=J1 AND ZIII$="  " THEN PRINT " ";:GOTO 4100
4090 INK COL:PRINT ZIII$;:INK 6
4100 IF J=J2 AND ZIII$="  " THEN PRINT CHR$(8);BCOL$;":"; ELSE IF J=J2 THEN PRINT ":";
4110 IF J<>J2 THEN PRINT "";
4120 J=J+3:IF J>J2 THEN GOTO 4130 ELSE GOTO 4060
4130 IF I=1 THEN PRINT"DAYS LEFT ";INT(T0+T9-T);
4140 IF I=2 THEN PRINT"CONDIT";
4145 IF I=2 AND CC$=CCOL$+"*RED*"+BCOL$ THEN INK 2 ELSE INK 6
4150 IF I=2 THEN PRINT CC$;:INK 6
4160 IF I=3 THEN PRINT"QUADRANT ";QI;",";QII;
4170 IF I=4 THEN PRINT"SECTOR   ";SI;",";SII;
4180 IF I=5 THEN PRINT"TORPEDOS ";INT(P);
4190 IF I=6 THEN PRINT"ENERGY   ";INT(E+S);
4200 IF I=7 THEN PRINT"SHIELDS  ";INT(S);
4210 REM IF I=7 AND S<201 AND K3>0 THEN INK 2:PRINT LEFT$(LOW$,3);:INK 6
4220 IF I=8 THEN PRINT"KLINGONS ";INT(K9);
4230 PRINT
4240 I=I+1:IF I>8 THEN GOTO 4250 ELSE GOTO 4020
4250 PRINT " +-+-+-+-+-+-+-+-+";
4260 MW=CI-CI+E/8:MW=MW*10:MW=INT(MW):MW=MW/10
4270 IF MW>8 THEN MW=8
4280 IF D(1)<0 AND MW>0.2 THEN MW=0.2
4290 PRINT"MAX WARP ";MW
4300 IF SLSFLAG=1 THEN GOTO 1900
4310 RETURN
4320 REM LIBRARY COMPUTER CODE
4330 IF D(8)<0 THEN PRINT:PRINT"COMPUTER DISABLED":GOTO 770
4340 PRINT:PRINT"FUNCTIONS AVA1LAB FROM COMPUTER:":PRINT
4350 PRINT" 0 - CUMULATIVE GALACTIC RECORD"
4360 PRINT" 1 - STATUS & DAMAGE REPORT"
4370 PRINT" 2 - PHOTON TORPEDO TARGET DATA"
4380 PRINT" 3 - STARBASE NAV DATA"
4390 PRINT" 4 - DIR/DISTANCE CALCULATOR"
4400 REM PRINT" 5 - GALAXY 'REGION NAME' MAP"
4410 PRINT:LX=1:PRINT"COMPUTER ACTIVE, AWAITING CMD: ";
4420 IF COMFLAG=1 THEN PRINT "0":AA=CI-CI:AI=CI-CI:GOTO 4460
4430 GOSUB 6100
4440 AA=VAL(LIIII$):AI=AA:IF AA<0 OR AA>4 THEN GOTO 860
4450 IF LIIII$="" THEN GOTO 860
4460 H8=1:ON AA GOTO 4510,4910,5050,5570,5120,4480
4470 REM SETUP TO CHANGE CUM GAL RECORD TO GALAXY MAP
4480 H8=0:G5=1:PRINT:PRINT"QUADRANT";QI;",";QII;"(";quadname$;")"
4490 PRINT:PRINT"   ";:GOTO 4540
4500 REM CUM GALACTIC RECORD
4510 PRINT:GALFLAG=0
4520 PRINT"COMPUTER RECORD OF GALAXY FOR":PRINT"QUADRANT ";QI;",";QII
4530 PRINT : PRINT " ";: INVERSE 1
4540 J=1
4550 JJJ$=STR$(J):JJJ$=RIGHT$(JJJ$,1)
4560 IF AI=5 THEN GOSUB 6490:GOTO 4580
4570 PRINT " ";JJJ$;" ";: IF J<8 THEN PRINT " ";
4580 J=J+1:IF J>8 THEN GOTO 4590 ELSE GOTO 4550
4590 INVERSE 0:PRINT;
4600 O1$="---+---+---+---+---+---+---+---"
4610 PRINT INVERSE 1;" "; INVERSE 0;O1$;:I=1
4620 III$=STR$(I):III$=RIGHT$(III$,1)
4630 IF I=QI THEN PRINT "";III$;
4640 IF AI=5 AND I=QI THEN GOTO 4760
4650 IF I<>QI THEN PRINT ""; INVERSE 1;III$;; INVERSE 0;:IF H8=0 THEN GOTO 4760
4660 J=1
4670 IF I=QI AND J=QII THEN GALFLAG=1:IF J<>1 THEN INK 5:PRINT "(";:INK 6:GOTO 4700
4680 IF I=QI AND J-1=QII THEN GOTO 4700
4690 IF J<>1 AND NOT GALFLAG THEN PRINT":";
4700 IF ZZ(I,J)=0 THEN PRINT"   ";:GOTO 4740
4710 G1$=RIGHT$(STR$(ZZ(I,J)+1000),3)
4720 G2$=MID$(G1$,1,3):PRINT G2$;
4730 IF GALFLAG=1 THEN GALFLAG=0: IF J<>8 INK 5:PRINT ")";:INK 6
4740 J=J+1:IF J>8 THEN GOTO 4750 ELSE GOTO 4670
4750 GOTO 4850
4760 Z4=I:Z5=1:GOSUB 5850:J0=INT(11-0.5*LEN(G2$))
4770 PRINT":";
4780 IF I=QI AND QII<5 THEN PRINT FCOL$;TAB(J0);G2$;BCOL$;:GOTO 4800
4790 PRINT TAB(J0);G2$;
4800 PRINT TAB(18);":";
4810 Z5=5:GOSUB 5850:J0=INT(27-0.5*LEN(G2$))
4820 IF I=QI AND QII>4 THEN PRINT FCOL$;TAB(J0);G2$;BCOL$;:GOTO 4840
4830 PRINT TAB(J0);G2$;
4840 PRINT TAB(34);":";
4850 IF AA=0 AND J=9 AND QI=I AND QII=8 THEN PRINT:GOTO 4870
4860 REM IF AA=0 THEN PRINT":"
4870 IF AA=5 THEN PRINT
4880 PRINT INVERSE 1;" ";INVERSE 0;O1$;:I=I+1:IF I>8 THEN GOTO 4890 ELSE GOTO 4620
4890 AI=0:GOTO 770
4900 REM STATUS
4910 PRINT:PRINT "STATUS REPORT:":XX$="":IF K9>1 THEN XX$="S"
4920 PRINT:PRINT" KLINGONS LEFT :";K9
4930 PRINT" ENERGY        :";INT(E+S)
4940 PRINT" TORPEDOES     :";INT(P)
4950 PRINT:PRINT"MISSION MUST BE COMPLETED IN"
4960 PRINT INT(T0+T9-T);" STARDATES"
4970 XX$="S":IF B9<2 THEN XX$="":IF B9<1 THEN GOTO 5010
4980 PRINT:PRINT"THE FEDERATION IS MA1NTA1NING "
4990 PRINT B9;" STARBASE";XX$;" IN THE GALAXY"
5000 GOTO 3110
5010 PRINT:PRINT"YOUR STUPIDITY HAS LEFT YOU ON YOUR OWN"
5020 PRINT"IN THE GALAXY -- YOU HAVE NO STARBASES"
5030 PRINT"LEFT!":GOTO 3110
5040 REM TORPEDO, BASE NAV, D/D CALCULATOR
5050 IF K3<=0 THEN GOTO 2260
5060 XX$="":IF K3>1 THEN XX$="S"
5070 PRINT:PRINT"FROM ENTERPRISE TO KLINGON SHIP";
5080 PRINT XX$
5090 H8=0:FOR I=1 TO 3:IF K(I,3)<=0 THEN GOTO 5560
5100 W1=K(I,1):X=K(I,2)
5110 CI=SI:AA=SII:GOTO 5240
5120 PRINT:PRINT"DIRECTION/DISTANCE CALCULATOR:"
5130 PRINT:PRINT"YOU ARE AT (Y,X):":PRINT "QUADRANT ";QI;",";QII;
5140 PRINT" SECTOR ";SI;",";SII
5150 PRINT:LX=4:PRINT"ENTER INIT. COORDINATES Y1= ";:GOSUB 6100
5160 CI=VAL(LIIII$):IF CI=0 THEN PRINT:PRINT"CALCULATION ABORTED!":GOTO 860
5170 LX=4:PRINT"ENTER INIT. COORDINATES X1= ";:GOSUB 6100
5180 AA=VAL(LIIII$):IF AA=0 THEN PRINT:PRINT"CALCULATION ABORTED!":GOTO 860
5190 PRINT:LX=4:PRINT"ENTER FINAL COORDINATES Y2= ";:GOSUB 6100
5200 W1=VAL(LIIII$):IF W1=0 THEN PRINT:PRINT"CALCULATION ABORTED!":GOTO 860
5210 LX=4:PRINT"ENTER FINAL COORDINATES X2= ";:GOSUB 6100
5220 X=VAL(LIIII$):IF X=0 THEN PRINT:PRINT"CALCULATION ABORTED!":GOTO 860
5230 IF CI=W1 AND AA=X THEN PRINT:PRINT"NO RESULTS POSSIBLE!":GOTO 860
5240 X=X-AA:AA=CI-W1:IF X<0 THEN GOTO 5380
5250 IF AA<0 THEN GOTO 5400
5260 IF X>0 THEN GOTO 5280
5270 IF AA=0 THEN CI=5:GOTO 5290
5280 CI=1
5290 IF ABS(AA)<=ABS(X) THEN GOTO 5340
5300 PRINT:PRINT" DIRECTION =";
5310 D2=CI+(((ABS(AA)-ABS(X))+ABS(AA))/ABS(AA))
5320 D2=D2*1000:D2=D2+0.5:D2=INT(D2):D2=D2/1000
5330 PRINT "";D2:GOTO 5500
5340 PRINT:PRINT" DIRECTION =";
5350 D2=CI+(ABS(AA)/ABS(X))
5360 D2=D2*1000:D2=D2+0.5:D2=INT(D2):D2=D2/1000
5370 PRINT "";D2:GOTO 5500
5380 IF AA>0 THEN CI=3:GOTO 5410
5390 IF X<>0 THEN CI=5:GOTO 5290
5400 CI=7
5410 IF ABS(AA)>=ABS(X) THEN GOTO 5460
5420 PRINT:PRINT" DIRECTION =";
5430 D2=CI+(((ABS(X)-ABS(AA))+ABS(X))/ABS(X))
5440 D2=D2*1000:D2=D2+0.5:D2=INT(D2):D2=D2/1000
5450 PRINT "";D2:GOTO 5500
5460 PRINT:PRINT" DIRECTION =";
5470 D2=CI+(ABS(X)/ABS(AA))
5480 D2=D2*1000:D2=D2+0.5:D2=INT(D2):D2=D2/1000
5490 PRINT "";D2
5500 PRINT" DISTANCE  =";
5510 D2=SQR((ABS(X))^2+(ABS(AA))^2)
5520 IF AI=3 OR AI=2 THEN D2=D2/10
5530 D2=D2*1000:D2=D2+0.5:D2=INT(D2):D2=D2/1000
5540 PRINT "";D2
5550 IF H8=1 THEN GOTO 770
5560 NEXT I:GOTO 770
5570 IF B3<>0 THEN PRINT:PRINT"FROM ENTERPRISE TO STARBASE":W1=B4:X=B5:GOTO 5110
5580 PRINT:PRINT"MR. SPOCK REPORTS, 'SENSORS SHOW NO ";
5590 PRINT"STARBASES IN THIS QUADRANT.'":GOTO 770
5600 REM FIND EMPTY PLACE (FOR THINGS)
5610 A$="   "
5620 R1=FNR():R2=FNR():Z1=R1:Z2=R2:GOSUB 5810:IF Z3=0 THEN GOTO 5610
5630 RETURN
5640 REM ARRAY FOR QUADRANT
5650 S8=INT(Z2-0.5)*3+INT(Z1-0.5)*24+1
5660 IF LEN(A$)<>3 THEN PRINT"ERROR":STOP
5670 IF S8=1 THEN Q$=A$+RIGHT$(Q$,189):RETURN
5680 IF S8=190 THEN Q$=LEFT$(Q$,189)+A$:RETURN
5690 Q$=LEFT$(Q$,S8-1)+A$+RIGHT$(Q$,190-S8):RETURN
5700 REM PRINTS DEVICE NAME
5710 ON R1-1 GOTO 5720,5730,5740,5750,5760,5770,5780,5790
5720 G2$="WARP ENGINES":RETURN
5730 G2$="SHORT RG SENSORS":RETURN
5740 G2$="LONG RG SENSORS":RETURN
5750 G2$="PHASER CONTROL":RETURN
5760 G2$="PHOTON TUBES":RETURN
5770 G2$="DAMAGE CONTROL":RETURN
5780 G2$="SHIELD CONTROL":RETURN
5790 G2$="LIBR.-COMPUTER":RETURN
5800 REM COMPARISON QUADRANT ARRAY
5810 Z1=INT(Z1+0.5):Z2=INT(Z2+0.5):S8=(Z2-1)*3+(Z1-1)*24+1:Z3=0
5820 IF MID$(Q$,S8,3)<>A$ THEN RETURN
5830 Z3=1:RETURN
5840 REM QUADRANT NAME IN G2$ FROM Z4,Z5 (=QI,QII)
5850 IF Z5<=4 THEN ON Z4-1 GOTO 5870,5880,5890,5900,5910,5920,5930,5940
5860 GOTO 5950
5870 G2$="ANTARES":GOTO 6040
5880 G2$="RIGEL":GOTO 6040
5890 G2$="PROCYON":GOTO 6040
5900 G2$="VEGA":GOTO 6040
5910 G2$="CANOPUS":GOTO 6040
5920 G2$="ALTA1R":GOTO 6040
5930 G2$="SAGITTARIUS":GOTO 6040
5940 G2$="POLLUX":GOTO 6040
5950 ON Z4-1 GOTO 5960,5970,5980,5990,6000,6010,6020,6030
5960 G2$="SIRIUS":GOTO 6040
5970 G2$="DENEB":GOTO 6040
5980 G2$="CAPELLA":GOTO 6040
5990 G2$="BETELGEUSE":GOTO 6040
6000 G2$="ALDEBARAN":GOTO 6040
6010 G2$="REGULUS":GOTO 6040
6020 G2$="ARCTURUS":GOTO 6040
6030 G2$="SPICA"
6040 IF G5<>1 THEN ON Z5-1 GOTO 6060,6070,6080,6090,6060,6070,6080,6090
6050 RETURN
6060 G2$=G2$+" I":RETURN
6070 G2$=G2$+" II":RETURN
6080 G2$=G2$+" III":RETURN
6090 G2$=G2$+" IV":RETURN
6100 REM LX = MAX INPUT LIIII$ = OUTPUT
6110 GOSUB 6770:RETURN
6120 REM * PAUSE WITHOUT CR
6130 CR$="PRESS RETURN TO CONTINUE "
6140 LX=0:PRINT CR$;:GOSUB 6100
6150 RETURN
6160 REM * KEY TO SRS ICONS
6170 PRINT:PRINT "KEY TO SHORT RANGE SCANER ICONS:":PRINT
6180 INK 5:PRINT FCOL$;"E";BCOL$;:INK 6:PRINT "= THE USS ENTERPRISE"
6190 INK 4:PRINT DCOL$;"B";BCOL$;:INK 6:PRINT "= FEDERATION STARBASE"
6200 PRINT HCOL$;"*";BCOL$;"= STAR"
6210 INK 2:PRINT ECOL$;"K";BCOL$;:INK 6:PRINT"= KLINGON BATTLE CRUISER"
6220 RETURN
6230 REM * COMMANDS
6240 PRINT : PRINT "USE THESE COMMANDS:": PRINT 
6250 PRINT " NAV -TO SET COURSE"
6260 PRINT " SRS -FOR SHORT RANGE SCAN"
6270 PRINT " LRS -FOR LONG RANGE SCAN"
6280 PRINT " SLS -FOR SHORT,LONG RANGE SCAN"
6290 PRINT " GAL -NAVIG.GALAXY (VIA COMPUTE"
6300 PRINT " PHA -TO FIRE PHASERS"
6310 PRINT " TOR -TO FIRE PHOTON TORPEDOES"
6320 PRINT " SHE -TO RAISE OR LOWER SHIELDS"
6330 PRINT " DAM -FOR DAMAGE CONTRL REPORTS"
6340 PRINT " COM -TO CALL ON LIBR-COMPUTER"
6350 PRINT " KEY -DISPLAY 'KEY' TO SRS ICON"
6360 PRINT " HLP -THIS LIST OF COMMANDS"
6370 PRINT " XXX -TO RESIGN YOUR COMMAND"
6380 RETURN
6390 REM * DIRECTION
6400 PRINT
6410 PRINT " ENTER A NUMERIC      4  3  2"
6420 PRINT " BETWEEN 1 AND 9       . . ."
6430 PRINT "                        ..."
6440 PRINT " DECIMALS MAY BE    5 ---*--- 1"
6450 PRINT " USED, IE N.nnn         ..."
6460 PRINT "                       . . ."
6470 PRINT "                      6  7  8"
6480 RETURN
6490 REM * ROMAN NUMERALS
6500 IF J=1 OR J=5 THEN PRINT " I ";;
6510 IF J=2 OR J=6 THEN PRINT "II ";
6520 IF J=3 OR J=7 THEN PRINT "III";
6530 IF J=4 OR J=8 THEN PRINT "IV ";
6540 PRINT " ";:RETURN
6550 REM * INITIALISATION & COLOURS
6560 CLS
6570 BCOL$=CHR$(32):REM WHITE
6580 CCOL$=CHR$(32):REM RED
6590 DCOL$=CHR$(32):REM CYAN
6600 ECOL$=CHR$(32):REM VIOLET
6610 FCOL$=CHR$(32):REM GREEN
6620 HCOL$=CHR$(32):REM YELLOW
6630 ICOL$=CHR$(32):REM BLUE
REM 6640 MODE 1:INK 0,0
6650 ZIIII$="":ATAKFLAG=0:SLSFLAG=0
6760 RETURN
6770 REM * LINPUT (LX=MAX LENGTH)
6780 iIII$="":x%=LX:PRINT "_";CHR$(8);
6790 III$=INKEY$
6800 IF III$="" THEN GOTO 6790
6805 IF INKEY$<>"" THEN GOTO 6805
6810 IF CODE(III$)=12 THEN GOTO 6860
6820 IF CODE(III$)=13 AND x%=0 THEN PRINT"...":LIIII$=iIII$:RETURN
6830 IF CODE(III$)=13 THEN PRINT " ";:PRINT:LIIII$=iIII$:RETURN
6840 IF CODE(III$)=32 THEN GOTO 6880
6850 IF CODE(III$)<46 OR VAL(III$)>90 THEN GOTO 6790
6860 IF CODE(III$)=12 AND LEN(iIII$)<1 THEN GOTO 6790
6870 IF III$=CHR$(12) THEN III$="":iIII$=LEFT$(iIII$,(LEN(iIII$)-1)):PRINT CHR$(8);
6880 IF LEN(iIII$)<x% THEN PRINT III$;"_ ";CHR$(8);CHR$(8);
6890 IF LEN(iIII$)<x% THEN iIII$=iIII$+III$
6900 GOTO 6790
